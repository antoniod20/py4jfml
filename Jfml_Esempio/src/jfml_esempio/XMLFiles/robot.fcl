/*
	Robot controller.
	
	Reference: M. Mucientes, R. Alcala, J. Alcala-Fdez, J. Casillas. Learning Weighted Linguistic Rules to Control an Autonomous Robot. International Journal of Intelligent Systems 24 (2009) 226–251. 

	Jesus Alcala-Fdez 
	jalcala@decsai.ugr.es
*/
FUNCTION_BLOCK robot

VAR_INPUT
	rd : REAL;			// Right distance
	dq : REAL;			// Distance quotient
	o  : REAL;			// Orientation. Note: I cannot use 'or' (like the reference paper) because it is a reserved word in FCL
	v  : REAL;			// Velocity
END_VAR

VAR_OUTPUT
	la : REAL;			// Linear acceleration
	av : REAL;			// Angular velocity
END_VAR

FUZZIFY rd
	TERM L  := (0, 1) (1, 0);
	TERM M  := (0, 0) (1, 1) (2, 0);
	TERM H  := (1, 0) (2, 1) (3, 0);
	TERM VH := (2, 0) (3, 1);
END_FUZZIFY

FUZZIFY dq
	TERM L := (0, 1) (2, 0);
	TERM H := (0, 0) (2, 1);
END_FUZZIFY

FUZZIFY o
	TERM HL := (-45.0, 1) (-22.5, 0);
	TERM LL := (-45.0, 0) (-22.5, 1) (0, 0);
	TERM Z  := (-22.5, 0) (0, 1) (22.5, 0);
	TERM LR := (0, 0) (22.5, 1) (45.0, 0);
	TERM HR := (22.5, 0) (45.0, 1);
END_FUZZIFY

FUZZIFY v
	TERM L := (0, 1) (1, 0);
	TERM H := (0, 0) (1, 1);
END_FUZZIFY

DEFUZZIFY la
	TERM VHB := -1;
	TERM HB  := -0.75;
	TERM MB  := -0.5;
	TERM SB  := -0.25;
	TERM Z   := 0;
	TERM SA  := 0.25;
	TERM MA  := 0.5;
	TERM HA  := 0.75;
	TERM VHA := 1;
	RANGE := (-1 .. 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY av
	TERM VHR := -1;
	TERM HR  := -0.75;
	TERM MR  := -0.5;
	TERM SR  := -0.25;
	TERM Z   := 0;
	TERM SL  := 0.25;
	TERM ML  := 0.5;
	TERM HL  := 0.75;
	TERM VHL := 1;
	RANGE := (-1 .. 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK rules
	AND  : MIN;			// Use 'min' for 'AND' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT  : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

    RULE 01:    IF rd IS  L AND dq IS L AND o IS LL AND v IS L THEN la IS VHB , av IS VHR WITH 0.4610;
    RULE 02:    IF rd IS  L AND dq IS L AND o IS LL AND v IS H THEN la IS VHB , av IS VHR WITH 0.4896;
    RULE 03:    IF rd IS  L AND dq IS L AND o IS  Z AND v IS L THEN la IS   Z , av IS  MR WITH 0.6664;
    RULE 04:    IF rd IS  L AND dq IS L AND o IS  Z AND v IS H THEN la IS  HB , av IS  SR WITH 0.5435;
    RULE 05:    IF rd IS  L AND dq IS H AND o IS LL AND v IS L THEN la IS  MA , av IS  HR WITH 0.7276;
    RULE 06:    IF rd IS  L AND dq IS H AND o IS  Z AND v IS L THEN la IS  MA , av IS  HL WITH 0.4845;
    RULE 07:    IF rd IS  L AND dq IS H AND o IS  Z AND v IS H THEN la IS  HB , av IS  ML WITH 0.5023;
    RULE 08:    IF rd IS  L AND dq IS H AND o IS LR AND v IS H THEN la IS VHB , av IS VHL WITH 0.7363;
    RULE 09:    IF rd IS  L AND dq IS H AND o IS HR AND v IS L THEN la IS VHB , av IS VHL WITH 0.9441;
    RULE 10:    IF rd IS  M AND dq IS L AND o IS  Z AND v IS H THEN la IS  SA , av IS  HR WITH 0.3402;
    RULE 11:    IF rd IS  M AND dq IS L AND o IS LR AND v IS H THEN la IS   Z , av IS VHL WITH 0.4244;
    RULE 12:    IF rd IS  M AND dq IS L AND o IS HR AND v IS L THEN la IS  SA , av IS  HL WITH 0.5472;
    RULE 13:    IF rd IS  M AND dq IS L AND o IS HR AND v IS H THEN la IS  MB , av IS VHL WITH 0.4369;
    RULE 14:    IF rd IS  M AND dq IS H AND o IS HL AND v IS L THEN la IS   Z , av IS VHR WITH 0.1770;
    RULE 15:    IF rd IS  M AND dq IS H AND o IS HL AND v IS H THEN la IS VHB , av IS VHR WITH 0.4526;
    RULE 16:    IF rd IS  M AND dq IS H AND o IS LL AND v IS H THEN la IS  SA , av IS VHR WITH 0.2548;
    RULE 17:    IF rd IS  M AND dq IS H AND o IS  Z AND v IS L THEN la IS  HA , av IS   Z WITH 0.2084;
    RULE 18:    IF rd IS  M AND dq IS H AND o IS LR AND v IS L THEN la IS  HA , av IS VHL WITH 0.6242;
    RULE 19:    IF rd IS  M AND dq IS H AND o IS LR AND v IS H THEN la IS  SA , av IS VHL WITH 0.3779;
    RULE 20:    IF rd IS  M AND dq IS H AND o IS HR AND v IS L THEN la IS   Z , av IS VHL WITH 0.6931;
    RULE 21:    IF rd IS  M AND dq IS H AND o IS HR AND v IS H THEN la IS VHB , av IS VHL WITH 0.7580;
    RULE 22:    IF rd IS  H AND dq IS L AND o IS  Z AND v IS L THEN la IS  HA , av IS VHR WITH 0.5758;
    RULE 23:    IF rd IS  H AND dq IS L AND o IS LR AND v IS H THEN la IS  SA , av IS  MR WITH 0.2513;
    RULE 24:    IF rd IS  H AND dq IS L AND o IS HR AND v IS L THEN la IS  HA , av IS VHL WITH 0.5471;
    RULE 25:    IF rd IS  H AND dq IS L AND o IS HR AND v IS H THEN la IS  SA , av IS  HL WITH 0.5595;
    RULE 26:    IF rd IS  H AND dq IS H AND o IS HL AND v IS L THEN la IS VHB , av IS VHR WITH 0.9999;
    RULE 27:    IF rd IS  H AND dq IS H AND o IS HL AND v IS H THEN la IS VHB , av IS VHR WITH 0.9563;
    RULE 28:    IF rd IS  H AND dq IS H AND o IS LL AND v IS L THEN la IS  HA , av IS VHR WITH 0.9506;
    RULE 29:    IF rd IS  H AND dq IS H AND o IS  Z AND v IS L THEN la IS  HA , av IS VHR WITH 0.4529;
    RULE 30:    IF rd IS  H AND dq IS H AND o IS  Z AND v IS H THEN la IS  SA , av IS VHR WITH 0.2210;
    RULE 31:    IF rd IS  H AND dq IS H AND o IS LR AND v IS L THEN la IS  HA , av IS  MR WITH 0.3612;
    RULE 32:    IF rd IS  H AND dq IS H AND o IS LR AND v IS H THEN la IS  SA , av IS  MR WITH 0.2122;
    RULE 33:    IF rd IS  H AND dq IS H AND o IS HR AND v IS L THEN la IS  HA , av IS  HL WITH 0.7878;
    RULE 34:    IF rd IS  H AND dq IS H AND o IS HR AND v IS H THEN la IS  SA , av IS VHL WITH 0.3859;
    RULE 35:    IF rd IS VH AND dq IS L AND o IS LR AND v IS L THEN la IS  HA , av IS VHR WITH 0.5530;
    RULE 36:    IF rd IS VH AND dq IS L AND o IS HR AND v IS L THEN la IS  HA , av IS  HR WITH 0.4223;
    RULE 37:    IF rd IS VH AND dq IS L AND o IS HR AND v IS H THEN la IS  SA , av IS  HR WITH 0.3854;
    RULE 38:    IF rd IS VH AND dq IS H AND o IS LL AND v IS L THEN la IS  HA , av IS VHR WITH 0.0936;
    RULE 39:    IF rd IS VH AND dq IS H AND o IS LR AND v IS L THEN la IS  HA , av IS VHR WITH 0.7325;
    RULE 40:    IF rd IS VH AND dq IS H AND o IS LR AND v IS H THEN la IS  SA , av IS VHR WITH 0.5631;
    RULE 41:    IF rd IS VH AND dq IS H AND o IS HR AND v IS L THEN la IS  HA , av IS  HR WITH 0.5146;
END_RULEBLOCK

END_FUNCTION_BLOCK

